plugins {
    id "idea"
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
}

[compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "11"

ext {
    apacheCodedVersion = "1.15"
    assertjVersion = "3.22.0"
    junitVersion = "5.8.2"
    junitPlatformVersion = "1.8.2"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "commons-codec:commons-codec:$apacheCodedVersion"

    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitPlatformVersion"
}

test {
    useJUnitPlatform()
}

jar {
    archiveFileName = "${project.name}.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                "Main-Class": "ru.romanow.encryptor.PasswordEncryptorApplicationKt"
        )
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}